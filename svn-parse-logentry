#!/usr/bin/awk -f

BEGIN {
    FS = " \\| "
    fileCnt = 0
    descriptionCnt = 0
    isCaptureDescription = 0
    isCaptureSummary = 1
}

function quote(string)
{
    gsub(/'/, "'\\''", string)
    return "'" string "'"
}
function join(array, start, end,      result, i)
{
    result = quote(array[start])
    for (i = start + 1; i <= end; i++)
	result = result " " quote(array[i])
    return result
}


# Capture the metadata.
/^r[0-9]+/ {
    scalar["revision"] = $1
    scalar["author"] = $2
    scalar["timestampFull"] = $3
    scalar["timestamp"] = gensub(/ \([^()]+\)$/, "", "", $3)
    scalar["date"] = gensub(/ .*$/, "", "", $3)
    scalar["time"] = gensub(/^[^ ]+ ([^ ]+) .*$/, "\\1", "", $3)
    next
}

# Capture the changed file list.
/^Changed paths:/,/^$/ {
    if ($0 == "Changed paths:") next
    if ($0 == "") {
	isCaptureDescription = 1
	next
    }

    list["changes"][fileCnt] = $0
    list["paths"][fileCnt] = substr($0, 6)
    fileCnt += 1
    next
}
/^$/ {
    if (! isCaptureDescription) {
	isCaptureDescription = 1
	next
    } else if (isCaptureSummary) {
	isCaptureSummary = 0
	next
    }
}

# Capture the summary and description.
{
    if (isCaptureDescription) {
	if (isCaptureSummary) {
	    if (scalar["summary"] == "")
		scalar["summary"] = $0
	    else
		scalar["summary"] = scalar["summary"] " " $0
	} else {
	    list["description"][descriptionCnt] = $0
	    descriptionCnt += 1
	}
    }
}

END {
    for (s in scalar) {
	print s "=" quote(scalar[s])
    }

    # Prune the end boilerplace from the captured description.
    if (descriptionCnt > 1) {
	if (list["description"][descriptionCnt - 1] ~ /^-+$/) {
	    delete list["description"][descriptionCnt - 1]
	    descriptionCnt -= 1

	    if (list["description"][descriptionCnt - 1] == "") {
		delete list["description"][descriptionCnt - 1]
		descriptionCnt -= 1
	    }
	}
    }

    for (l in list) {
	len = length(list[l])
	print "typeset -a " l "=(" join(list[l], 0, len - 1) ")"
    }
}
